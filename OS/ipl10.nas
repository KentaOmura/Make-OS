; hello-os
; TAB=4
CYLS	EQU		10				; どこまで読み込むか

		ORG		0x7c00			; このプログラムがどこに読み込まれるのか

; 以下は標準的なFAT12フォーマットフロッピーディスクのための記述

		JMP		entry
		DB		0x90
		DB		"HARIBOTE"		; ブートセクタの名前を自由に書いてよい（8バイト）
		DW		512				; 1セクタの大きさ（512にしなければいけない）
		DB		1				; クラスタの大きさ（1セクタにしなければいけない）
		DW		1				; FATがどこから始まるか（普通は1セクタ目からにする）
		DB		2				; FATの個数（2にしなければいけない）
		DW		224				; ルートディレクトリ領域の大きさ（普通は224エントリにする）
		DW		2880			; このドライブの大きさ（2880セクタにしなければいけない）
		DB		0xf0			; メディアのタイプ（0xf0にしなければいけない）
		DW		9				; FAT領域の長さ（9セクタにしなければいけない）
		DW		18				; 1トラックにいくつのセクタがあるか（18にしなければいけない）
		DW		2				; ヘッドの数（2にしなければいけない）
		DD		0				; パーティションを使ってないのでここは必ず0
		DD		2880			; このドライブ大きさをもう一度書く
		DB		0,0,0x29		; よくわからないけどこの値にしておくといいらしい
		DD		0xffffffff		; たぶんボリュームシリアル番号
		DB		"HARIBOTEOS "	; ディスクの名前（11バイト）
		DB		"FAT12   "		; フォーマットの名前（8バイト）
		RESB	18				; とりあえず18バイトあけておく

; プログラム本体

entry:
		MOV		AX,0			; レジスタ初期化
		MOV		SS,AX
		MOV		SP,0x7c00
		MOV		DS,AX

; ディスクを読む
		MOV		AX,0x0820
		MOV		ES,AX
		MOV		CH,0			; シリンダ0
		MOV		DH,0			; ヘッド0
		MOV		CL,2			; セクタ2 本プログラム自体がセクタ1に所属する。その為、次のセクタから読み取る
readloop:
		MOV		AH,0x02
		MOV		AL,1
		MOV		BX,0
		MOV		DL,0x00			; Aドライブ
		INT		0x13
		JNC		next
next:
		MOV		AX,ES
		ADD		AX,0x0020		; 512バイト先に読み込むように設定
		MOV		ES,AX
		ADD		CL,1			; セクタを1つ進める。フロッピーであれば1セクタは512バイト
		CMP		CL,18			; 1つのシリンダに18セクタ存在する。
		JBE		readloop
		MOV		CL, 1
		ADD		DH, 1			; 裏のディスクを読み取っていく
		CMP		DH, 2			; 上下ヘッドが2つなので
		JB		readloop
		MOV		DH, 0			; 表に切り替え
		ADD		CH, 1			; 読み込むシリンダを進める
		CMP		CH, CYLS
		JB		readloop		; CYLSの定義値よりシリンダ数が少ない場合

; 読み終わったのでharibote.sysを実行だ！

		MOV		[0x0ff0],CH		; IPLがどこまで読んだのかをメモ
		JMP		0xc200			; SYSTEMが読み込まれる箇所 ブートセクタは0x8000地点でSYSはその0x4200後ろ


		RESB	0x7dfe-$		; 0x7dfeまでを0x00で埋める命令
		
		DB		0x55, 0xaa
